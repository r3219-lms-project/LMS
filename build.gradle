plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

configure(subprojects) { project ->
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    group = 'ru.lms-project'
    version = '0.0.1-SNAPSHOT'

    java { toolchain { languageVersion = JavaLanguageVersion.of(21) } }

    repositories { mavenCentral() }

    def isBootModule = !(project.name in ['services','shared'])
    if (isBootModule) {
        apply plugin: 'org.springframework.boot'
        dependencies {
            testImplementation 'org.springframework.boot:spring-boot-starter-test'
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.34'
        annotationProcessor 'org.projectlombok:lombok:1.18.34'
        testCompileOnly 'org.projectlombok:lombok:1.18.34'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'
    }

    tasks.withType(Test) { useJUnitPlatform() }
}
